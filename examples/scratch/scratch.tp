// This file introduces the basic of the taype language.

data list = Nil | Cons int list

fn elem : int -> list -> bool = \y xs =>
  match xs with
  | Nil => False
  | Cons x xs' => if x == y then True else elem y xs'
  end

fn insert : int -> list -> list = \y xs =>
  match xs with
  | Nil => Cons y Nil
  | Cons x xs' => if y <= x then Cons y xs else Cons x (insert y xs')
  end

fn filter : (int -> bool) -> list -> list = \f xs =>
  match xs with
  | Nil => Nil
  | Cons x xs' =>
    let v = filter f xs' in
    if f x
    then Cons x v
    else v
  end

fn map : (int -> int) -> list -> list = \f xs =>
  match xs with
  | Nil => Nil
  | Cons x xs' => Cons (f x) (map f xs')
  end

// ----------------------------------------------------------------
// Oblivious list and its instances

obliv ~list (k : int) =
  if k == 0
  then unit
  else unit ~+ ~int ~* ~list (k-1)

fn ~list#s : (k : int) -> list -> ~list k = \k xs =>
  if k == 0
  then ()
  else match xs with
       | Nil => ~inl ()
       | Cons x xs' => ~inr ~(~int#s x, ~list#s (k-1) xs')
       end

fn' ~list#r : (k : int) -> ~list k -> list = \k =>
  if k == 0
  then \_ => Nil
  else \xs => ~match xs with
              | ~inl _ => Nil
              | ~inr ~(x, xs') => Cons (~int#r x) (~list#r (k-1) xs')
              end

fn ~list#Nil : unit -> #~list = \_ =>
  #(0, (() :: ~list 0))

fn ~list#Cons : ~int * #~list -> #~list = \p =>
  match p with
  (x, pxs) =>
    match pxs with
    #(k, xs) =>
      #(k+1, ((~inr ~(x, xs) : unit ~+ ~int ~* ~list k) :: ~list (k+1)))
    end
  end

fn ~list#match : #~list -> (unit -> 'a) -> (~int * #~list -> 'a) -> 'a =
  \pxs f1 f2 =>
    match pxs with
    #(k, xs) =>
      (if k == 0
       then \_ => f1 ()
       else \xs =>
         ~match xs with
         | ~inl _ => f1 ()
         | ~inr ~(x, xs') => f2 (x, #(k-1, xs'))
         end : ~list k -> 'a) xs
    end

fn ~list#view : list -> int = \xs =>
  match xs with
  | Nil => 0
  | Cons _ xs' => 1 + ~list#view xs'
  end

fn ~list#join : int -> int -> int = \x y => if x <= y then y else x

fn ~list#reshape : (k : int) -> (k' : int) -> ~list k -> ~list k' = \k k' =>
  if k' == 0
  then \_ => ()
  else
    if k == 0
    then \_ => ~inl ()
    else \xs =>
      ~match xs with
      | ~inl _ => ~inl ()
      | ~inr ~(x, xs') => ~inr ~(x, ~list#reshape (k-1) (k'-1) xs')
      end

// ----------------------------------------------------------------
// Oblivious functions by hand

fn ~elem' : (k : int) -> ~int -> ~list k -> ~bool = \k ~y =>
  if k == 0
  then \_ => ~bool#s False
  else \~xs =>
    ~match ~xs with
    | ~inl _ => ~bool#s False
    | ~inr ~(~x, ~xs') =>
      ~if ~x ~== ~y
      then ~bool#s True
      else ~elem' (k-1) ~y ~xs'
    end

fn ~insert' : (k : int) -> ~int -> ~list k -> ~list (k+1) = \k y =>
  if k == 0
  then \_ => ((~inr ~(y, ()) : unit ~+ ~int ~* unit) :: ~list (k+1))
  else \xs =>
    ~match xs with
    | ~inl _ => ((~inr ~(y, ~list#reshape 0 k (() :: ~list 0))
         : unit ~+ ~int ~* ~list k) :: ~list (k+1))
    | ~inr ~(x, xs') =>
      ~if y ~<= x
      then ((~inr ~(y, xs) : unit ~+ ~int ~* (unit ~+ ~int ~* ~list (k-1)))
             :: ~list (k+1))
      else ((~inr ~(x, ~insert' (k-1) y xs') : unit ~+ ~int ~* ~list (k-1+1))
             :: ~list (k+1))
    end

fn ~filter' : (k : int) -> (~int -> ~bool) -> ~list k -> ~list k = \k f =>
  if k == 0
  then \_ => ()
  else \xs =>
    ~match xs with
    | ~inl _ => ~inl ()
    | ~inr ~(x, xs') =>
      let v = ~filter' (k-1) f xs' in
      ~if f x
      then (~inr ~(x, v) : unit ~+ ~int ~* ~list (k-1))
      else (~list#reshape (k-1) k v :: unit ~+ ~int ~* ~list (k-1))
    end

fn ~test_filter' : (k : int) -> ~list k -> ~int -> ~list k = \k xs y =>
  ~filter' k (\x => x ~<= y) xs

fn ~map' : (k : int) -> (~int -> ~int) -> ~list k -> ~list k = \k f =>
  if k == 0
  then \_ => ()
  else \xs =>
    ~match xs with
    | ~inl _ => ~inl ()
    | ~inr ~(x, xs') =>
      ~inr ~(f x, ~map' (k-1) f xs')
    end

fn ~test_map' : (k : int) -> ~list k -> ~int -> ~list k = \k xs y =>
  ~map' k (\x => x ~+ y) xs

// ----------------------------------------------------------------
// Oblivious functions by preprocessors

fn ~elem : ~int -> #~list -> ~bool = \y xs =>
  %match #~list ~bool
    xs
    (\_ => %coerce bool ~bool False)
    (\p =>
      match p with
      (x, xs') =>
        %if ~bool ~bool (%== (~int -> ~int -> ~bool) x y)
          (\_ => %coerce bool ~bool True)
          (\_ => ~elem y xs')
      end)

fn ~insert : ~int -> #~list -> #~list = \y xs =>
  %match #~list #~list
    xs
    (\_ => %Cons #~list (y, %Nil #~list ()))
    (\p =>
      match p with
      (x, xs') =>
        %if ~bool #~list (%<= (~int -> ~int -> ~bool) y x)
          (\_ => %Cons #~list (y, xs))
          (\_ => %Cons #~list (x, ~insert y xs'))
      end)

fn ~filter : (~int -> ~bool) -> #~list -> #~list = \f xs =>
  %match #~list #~list
    xs
    (\_ => %Nil #~list ())
    (\p =>
      match p with
      (x, xs') =>
        let v = ~filter f xs' in
        %if ~bool #~list (f x)
          (\_ => %Cons #~list (x, v))
          (\_ => v)
      end)

fn ~test_filter : #~list -> ~int -> #~list = \xs y =>
  ~filter (\x => %<= (~int -> ~int -> ~bool) x y) xs

fn ~map : (~int -> ~int) -> #~list -> #~list = \f xs =>
  %match #~list #~list
    xs
    (\_ => %Nil #~list ())
    (\p =>
      match p with
      (x, xs') =>
        %Cons #~list (f x, ~map f xs')
      end)

fn ~test_map : #~list -> ~int -> #~list = \xs y =>
  ~map (\x => %+ (~int -> ~int -> ~int) x y) xs

// obliv ~list_ (k : int) =
//   if k == 0
//   then unit
//   else ~int ~* ~list_ (k-1)

// fn ~list_#s : (k : int) -> list -> ~list_ k = \k xs =>
//   if k == 0
//   then ()
//   else match xs with
//        | Nil => ~~
//        | Cons x xs' => ~(~int#s x, ~list_#s (k-1) xs')
//        end

// fn' ~list_#r : (k : int) -> ~list_ k -> list = \k =>
//   if k == 0
//   then \_ => Nil
//   else \xs => ~match xs with
//               ~(x, xs') => Cons (~int#r x) (~list_#r (k-1) xs')
//               end

// fn ~list_#Nil : unit -> #~list_ = \_ =>
//   #(0, (() :: ~list_ 0))

// fn ~list_#Cons : ~int * #~list_ -> #~list_ = \p =>
//   match p with
//   (x, pxs) =>
//     match pxs with
//     #(k, xs) =>
//       #(k+1, (~(x, xs) :: ~list_ (k+1)))
//     end
//   end
